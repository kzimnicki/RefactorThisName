import org.apache.tools.ant.filters.ReplaceTokens
group = 'gwtClient'
description = 'GWT Client'

apply plugin: 'war'

dependencies {
    providedCompile group: 'com.google.gwt', name: 'gwt-user', version:'2.4.0'
    providedCompile group: 'com.google.gwt', name: 'gwt-dev', version: '2.4.0'
}

task compileGwt (dependsOn: classes, type: JavaExec) {
    buildDir = "${project.buildDir}/gwt"
    extraDir = "${project.buildDir}/extra"

    inputs.source sourceSets.main.java.srcDirs
    inputs.dir sourceSets.main.output.resourcesDir
    outputs.dir buildDir

    // Workaround for incremental build (GRADLE-1483)
    outputs.upToDateSpec = new org.gradle.api.specs.AndSpec()

    doFirst {
        file(buildDir).mkdirs()
    }

    main = 'com.google.gwt.dev.Compiler'

    classpath {
        [
            sourceSets.main.java.srcDirs,           // Java source
            sourceSets.main.output.resourcesDir,    // Generated resources
            sourceSets.main.output.classesDir,      // Generated classes
            sourceSets.main.compileClasspath,       // Deps
        ]
    }

    args =
        [
            'cc.explain.ExplainCC', // Your GWT module
            '-war', buildDir,
            '-logLevel', 'INFO',
            '-localWorkers', '2',
            '-compileReport',
            '-extra', extraDir,
            // '-draftCompile' // Speeds up compile with 25%
        ]

    maxHeapSize = '256M'
}



task devMode() << {
  ant.java(classname:'com.google.gwt.dev.DevMode', failOnError: 'true', fork: 'true') {
    jvmarg(value: '-Xss1024k')
    jvmarg(value: '-Xms256M')
    jvmarg(value: '-Xmx256M')
    arg(line: '-war src/main/webapp')
    arg(line: '-startupUrl index.html')
    arg(line: '-port 8881')
    arg(line: '-logLevel DEBUG')
    // uncomment to bind to all IPs, not just 127.0.0.1
    // useful for debugging in a virtualized OS
    // arg(line: '-bindAddress 0.0.0.0')
    arg(value: 'cc.explain.ExplainCC')
    classpath {
      pathElement(location: "src/main/java")
      pathElement(path: configurations.compile.asPath)
    }
  }
}




war.dependsOn compileGwt
war {
    from compileGwt.buildDir
     eachFile {
          if (it.name == 'index.html') {
            it.filter(ReplaceTokens, tokens: ['tmstp': String.valueOf(System.currentTimeMillis())])
          }
     }
}
